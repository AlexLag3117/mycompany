MODULE Input;

NAMESPACE Utils;

FORM dialogQuantity 'Ввод количества'
    OBJECTS q = NUMERIC[16,2] PANEL
    PROPERTIES(q) quantity 'Введите количество :' = VALUE
;

DESIGN dialogQuantity {
    PROPERTY(quantity) { fontSize = 16; }
    PROPERTY(formOk()) { changeKey = 'ENTER'; }
}

META defineChangeInput (property, type, form, object)
    change###property () {
        INPUT s = STRING DO IF s THEN 
            IF (GROUP SUM 1 IF isISubstring(name(type p), s)) = 1 THEN
                property() <- GROUP MIN type p IF isISubstring(name(p), s);
            ELSE
                DIALOG form OBJECTS object INPUT NULL FILTERS isISubstring(name(object), s) OR NOT s DO
                    property() <- object;
    }
END

META defineChangeInput (property, cls, type, form, object)
    change###property (cls o) {
        INPUT s = STRING DO IF s THEN
            IF (GROUP SUM 1 IF isISubstring(name(type p), s)) = 1 THEN
                property(o) <- GROUP MIN type p IF isISubstring(name(p), s);
            ELSE
                DIALOG form OBJECTS object INPUT NULL FILTERS isISubstring(name(object), s) OR NOT s DO
                    property(o) <- object;
    }
END

// keyboard
META defineKeyboard(form, cls, obj, container, size, action, number)
    form##Press##number ''##number (cls o) {
        action(o, number);
    }
    EXTEND FORM form PROPERTIES form##Press##number(obj);
    DESIGN form {
        container {
            MOVE PROPERTY(form##Press##number(obj)) { fill = 1; fontSize = ''##size; focusable = FALSE; }
        }
    }
END

META defineKeyboard(form, cls, obj, container, size, action)
    @defineKeyboard(form, cls, obj, container, size, action, 0);
    @defineKeyboard(form, cls, obj, container, size, action, 1);
    @defineKeyboard(form, cls, obj, container, size, action, 2);
    @defineKeyboard(form, cls, obj, container, size, action, 3);
    @defineKeyboard(form, cls, obj, container, size, action, 4);
    @defineKeyboard(form, cls, obj, container, size, action, 5);
    @defineKeyboard(form, cls, obj, container, size, action, 6);
    @defineKeyboard(form, cls, obj, container, size, action, 7);
    @defineKeyboard(form, cls, obj, container, size, action, 8);
    @defineKeyboard(form, cls, obj, container, size, action, 9);
END

nameFilter 'Наименование' = DATA LOCAL ISTRING () CHARWIDTH 20;
clearNameFilter '' () { nameFilter() <- NULL; } IMAGE 'delete.png';

// keyboard decimal

keyboardDecimalInput = DATA LOCAL STRING();

META defineKeyboardDecimal (property, form, cls, obj, container, size)

    set###form###property (cls o, INTEGER i) {
        IF NOT amount(o) = NUMERIC(keyboardDecimalInput()) THEN keyboardDecimalInput() <- NULL;
        keyboardDecimalInput() <- CONCAT '', keyboardDecimalInput(), STRING(i);
        property(o) <- NUMERIC(keyboardDecimalInput());
    }
    
    DESIGN form {
        right {
            NEW keyboard {
                fill = 1;
                type = COLUMNS;
                columns = 3;
            }
        }
    }
    
    @defineKeyboard(form, cls, obj, container, size, set###form###property);
    
    form##PressD '.' (cls o) {
        IF NOT isSubstring(keyboardDecimalInput(), '.') THEN
            keyboardDecimalInput() <- keyboardDecimalInput() + '.'; 
    }
    
    form##PressC 'C' (cls o) {
        amount(o) <- NULL;
    }
    
    EXTEND FORM form
        PROPERTIES(obj) form##PressD, form##PressC
    
        EVENTS ON CHANGE obj { keyboardDecimalInput() <- NULL; }
    ;
    
    DESIGN form {
        container {
            MOVE PROPERTY(form##PressD(obj)) { fill = 1; fontSize = size; focusable = FALSE; };
            MOVE PROPERTY(form##Press0(obj));
            MOVE PROPERTY(form##PressC(obj)) { fill = 1; fontSize = size; focusable = FALSE; };
        }
    }

END