MODULE ItemUom;

REQUIRE Item;

NAMESPACE MasterData;

ratio 'Соотношение' = DATA NUMERIC[20,10] (Item, Uom);

EXTEND FORM item
    OBJECTS u = Uom
    PROPERTIES(u) READONLY name, id
    PROPERTIES ratio(i, u)
    
    FILTERS u != uom(i)
    
    FILTERGROUP ratio
        FILTER 'Используются' ratio(i, u)
;

DESIGN item {
    tabs {
        MOVE BOX(u) {
            caption = 'Единицы измерения';
        }
    }
}

// vendor
META defineDocumentVendorUom(doc)
    vendorUom = DATA Uom (doc##Line);
    nameVendorUom 'Ед. изм. поставщика' (doc##Line l) = name(vendorUom(l));

    WHEN LOCAL CHANGED(item(doc##Line l)) AND NOT ratio(item(l), vendorUom(l)) DO 
        vendorUom(l) <- NULL;
        
    CONSTRAINT vendorUom(doc##Line l) AND NOT ratio(item(l), vendorUom(l)) CHECKED BY vendorUom[doc##Line]
        MESSAGE 'Выбрана единица измерения поставщика, для которой не задано соотношение';
END

META defineDocumentVendorQuantity(doc)
    vendorQuantity 'Кол-во поставщика' = DATA NUMERIC[16,3] (doc##Line);
    WHEN LOCAL (CHANGED(quantity(doc##Line l)) OR CHANGED(vendorUom(l))) AND NOT CHANGED(vendorQuantity(l)) DO
        vendorQuantity(l) <- NUMERIC[16,3](quantity(l) / ratio(item(l), vendorUom(l)));
    
    changeVendorQuantity (doc##Line l) {
        INPUT n = vendorQuantity(l) CHANGE DO {
            quantity(l) <- NUMERIC[16,3](vendorQuantity(l) * ratio(item(l), vendorUom(l)));
        }
    }
END

META defineDocumentVendorPrice(doc)
    vendorPrice 'Цена поставщика' = DATA NUMERIC[10,2] (doc##Line);
    WHEN LOCAL (CHANGED(price(doc##Line l)) OR CHANGED(vendorUom(l))) AND NOT CHANGED(vendorPrice(l)) DO
        vendorPrice(l) <- NUMERIC[10,2](price(l) * ratio(item(l), vendorUom(l)));
    
    changeVendorPrice (doc##Line l) {
        INPUT n = vendorPrice(l) CHANGE DO {
            price(l) <- NUMERIC[10,2](vendorPrice(l) / ratio(item(l), vendorUom(l)));
        }
    }
END