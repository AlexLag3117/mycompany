MODULE ItemHistory;

REQUIRE Authentication, Time;

NAMESPACE MasterData;

META defineItemHistory(it)

    @defineItemHistoryHead(it);
    
    WHEN DROPPED(Item it IS Item) DO {     
        NEW h = ItemHistory {
         item(h) <- it;
         dateTime(h) <- currentDateTime();
         user(h) <- currentUser();
         type(h) <- 'Удалена номенклатура';
         description (h) <- 'Удалена номенклатура ' + OVERRIDE PREV(name(it)), '';
        }
    }       
    
    WHEN SET(Item it IS Item)  DO {     
        NEW h = ItemHistory {
            item(h) <- it;
            dateTime(h) <- currentDateTime();
            user(h) <- currentUser();
            type(h) <- 'Добавлена номенклатура';
            description (h) <- 'Добавлена номенклатура ' + OVERRIDE name(it), '';
        }
    }
    
    EXTEND FORM history
        FILTERGROUP itemType
           FILTER 'Добавлена номенклатура' type(iteml) = 'Добавлена номенклатура';
           
    @defineItemHistoryFilter('удалена номенклатура');
        
END

META defineItemHistoryAttr(attr, caption)

    WHEN SETCHANGED(attr(Item item)) DO {     
        NEW l = ItemHistory {
            item(l) <- item;
            dateTime(l) <- currentDateTime();
            user(l) <- currentUser();
            type(l) <- ###caption;
            description (l) <- ###caption + ' на ' + attr(item);
        }
    }
    
    @defineItemHistoryFilter(caption);
    
END

META defineItemHistoryHead(it)
    
    CLASS ItemHistory 'История изменений';
    
    item = DATA Item (ItemHistory);

    dateTime 'Дата' = DATA DATETIME (ItemHistory);
    user 'Пользователь' = DATA User (ItemHistory) CHARWIDTH 15;
    nameUser 'Пользователь' (ItemHistory l) = name(user(l));
    type 'Тип' = DATA ISTRING[100] (ItemHistory);
    description 'Описание' = DATA TEXT (ItemHistory);
     
    EXTEND FORM item
        OBJECTS iteml = ItemHistory
        PROPERTIES(iteml) READONLY dateTime, nameUser, type, description, pdescription = description PANEL
        FILTERS item(iteml) = it
    ;
    
    FORM history 'История'
        OBJECTS iteml = ItemHistory
        PROPERTIES(iteml) READONLY dateTime, nameUser, type, description, pdescription = description PANEL
        ORDERS dateTime(iteml)
        ;

    showHistory 'История' () {
        SHOW history DOCKED;        
    }
    
    DESIGN item {
        tabs {
            NEW history {
                caption = 'История';
                fill = 1;
                type = SPLITH;
                MOVE BOX(iteml) { 
                    fill = 2;
                    PROPERTY(description(iteml)) { valueHeight = 18; }
                }
                MOVE PROPERTY(pdescription) { panelCaptionAbove = TRUE; fill = 1; }
            }
        }
    }  
     
    DESIGN history {
        OBJECTS {
            NEW pane {
                type = SPLITH;
                fill = 1;
                MOVE BOX(iteml) { 
                    //fill = 2;
                    PROPERTY(description(iteml)) { valueHeight = 18;}
                }
                MOVE PROPERTY(pdescription) { panelCaptionAbove = TRUE; fill = 1; }
    
            }
        }
    }
  
    
    EXTEND FORM items
        PROPERTIES showHistory() TOOLBAR 
    ; 
    
END

META defineItemHistoryFilter(t)
    EXTEND FORM history
        EXTEND FILTERGROUP itemType
           FILTER ###t type(iteml) = ###t;
END

